# -*- Mode: zsh -*-
#
# Fire theme lifted from oh-my-zsh fire_prompt and modified.

autoload -U colors && colors
autoload -Uz vcs_info

precmd() {
    vcs_info
}

setopt prompt_subst

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' formats "%r/%S (%b) %u%c %7.7i"
zstyle ':vcs_info:*' actionformats "%r/%S (%b) (%a) %u%c %7.7i"
zstyle ':vcs_info:*' get-revision true
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr "%F{green}●%F{white}"
zstyle ':vcs_info:*' unstagedstr "%F{yellow}●%F{white}"
zstyle ':vcs_info:git+set-message:*' hooks check-untracked

+vi-check-untracked() {
    [[ -n $(git ls-files --others --exclude-standard 2>&-) ]] &&
    hook_com[unstaged]="%F{red}●%F{white}${hook_com[unstaged]}"
}

local -a schars
autoload -Uz prompt_special_chars
prompt_special_chars

local fire1='yellow'
local fire2='yellow'
local fire3='red'
local phost='white'
local pdate='white'
local pcwd='yellow'

local GRAD0="%b%f%k"
local GRAD1="%{$schars[333]$schars[262]$schars[261]$schars[260]%}"
local GRAD2="%{$schars[260]$schars[261]$schars[262]$schars[333]%}"
local COLOR1="%B%F{$fire1}%K{$fire2}"
local COLOR2="%B%F{$phost}%K{$fire2}"
local COLOR3="%b%F{$fire3}%K{$fire2}"
local COLOR4="%b%F{$fire3}%K{black}"
local COLOR5="%B%F{$pcwd}%K{black}"
local COLOR6="%B%F{$pdate}%K{black}"

PS1=$COLOR1$GRAD1$COLOR2'%n@%m'$COLOR3$GRAD2$COLOR4$GRAD1$COLOR6'%D{%A} ${vcs_info_msg_0_}'$'\n'$COLOR5'%~'$GRAD0' %(?..%B%K{red}[%?]%K{gray}%{$reset_color%} )↳ '
PS2=$COLOR1$GRAD1$COLOR3$GRAD2$COLOR4$GRAD1$COLOR5'>'$GRAD0' '
RPS1='%D{%T}'

